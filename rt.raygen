#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_buffer_reference : require

#include "common.glsl"

layout(set = 0, binding = 2, rgba8) uniform image2D kTextures2DInOut[];
layout(set = 0, binding = 2, rgba16f) uniform image2D kTextures2DInOutF16[];
layout(set = 0, binding = 4) uniform accelerationStructureEXT kTLAS[];

layout(std430, buffer_reference) readonly buffer LightsBuffer;
layout(std430, buffer_reference) readonly buffer PointLightsBuffer;

layout(push_constant) uniform PushConstants {
    uint tlas;
    uint skyboxTexture;
    uint outputTexture;
    uint firstHitTexture;
    uint secondHitTexture;
    uint velocityTexture;
    CameraBuffer camera;
    TransformsBuffer transforms;
    PrevTransformsBuffer prevTransforms;
    InstanceDataBuffer instanceData;
    VertexBuffer vertexBuffer;
    IndexBuffer indexBuffer;
    MaterialBuffer materials;
    LightsBuffer lights;
    PointLightsBuffer pointLights;
};

layout(location = 0) rayPayloadEXT RayPayload payload;

void main() {
    vec2 pixelCenter = gl_LaunchIDEXT.xy + vec2(0.5);
    vec2 d = 2.0 * (pixelCenter / gl_LaunchSizeEXT.xy) - 1.0;
    d.y = -d.y;

    vec4 origin    = camera.viewInverse * vec4(0,0,0,1);
    vec4 target    = camera.projectionInverse * vec4(d, 1, 1);
    vec4 direction = camera.viewInverse * vec4(normalize(target.xyz), 0);

    payload.color = vec3(0.0);
    payload.depth = 0;

    // Initialize hit textures to "no hit" state
    ivec2 pixelCoord = ivec2(gl_LaunchIDEXT.xy);
    imageStore(kTextures2DInOutF16[firstHitTexture], pixelCoord, vec4(0.0, 0.0, 0.0, -1.0));
    imageStore(kTextures2DInOutF16[secondHitTexture], pixelCoord, vec4(0.0, 0.0, 0.0, -1.0));
    imageStore(kTextures2DInOutF16[velocityTexture], pixelCoord, vec4(0.0));

    traceRayEXT(kTLAS[tlas], gl_RayFlagsNoneEXT, 0xff, 0, 0, 0, origin.xyz, camera.near, direction.xyz, camera.far, 0);

    imageStore(kTextures2DInOut[outputTexture], pixelCoord, vec4(payload.color, 1.0));
}
